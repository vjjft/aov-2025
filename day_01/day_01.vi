
pub fn main(&io: &IO) {
  let position = 50;
  let running = position;
  let hits = 0;
  let passes = 0;

  while io.read_line() is Some(turn) {
    assert turn!.head_tail() is Some(dir, num) else { debug::error("bad input") }
    assert N32::parse(String(num)) is Some(n) else { debug::error("bad input") }

    let delta = when {
      dir == 'L' { -n }
      _ { +n }
    };

    let spins = n / 100;
    passes += spins;

    let start = position;
    let temp = position as I32;

    temp += delta % +100;

    if start != 0 {
      when {
        temp <= +0 { passes += 1 }
        temp >= +100 {
          passes += 1;
        }
      }
    }

    position = ((temp + +100) % +100) as N32;

    if (position == 0) {
      hits += 1;
    }
    // (start, delta, spins, temp, position, (hits, passes)).dbg();
  };

  let part1 = hits;
  io.println("Part 1: {part1}");

  let part2 = passes;
  io.println("Part 2: {part2}");
}
